\import Paths
\import Logic

\data \fixr 2 Or (A B : \Type)
  | inl A
  | inr B
  \where {
    \func levelProp {A B : \Prop} (e : A -> B -> Empty) (x y : Or A B) : x = y \elim x, y
      | inl a, inl a' => pmap inl (Path.inProp a a')
      | inl a, inr b => absurd (e a b)
      | inr b, inl a => absurd (e a b)
      | inr b, inr b' => pmap inr (Path.inProp b b')

    \func map {A B C D : \Type} (f : A -> C) (g : B -> D) (t : A `Or` B) : C `Or` D \elim t
      | inl a => inl (f a)
      | inr b => inr (g b)
  }
